 r←{cbinfo}SendWait(cid u);i;uid;payload;z;fn;poll
 :If 0=⎕NC'cbinfo' ⋄ cbinfo←'' 0 ⋄ :EndIf
 (fn poll)←cbinfo

 ('Callback function ',fn,' not found')⎕SIGNAL(3≠⎕NC fn)/6

 :Hold 'hmon'
     :If (2=≢u)∧326=⎕DR u
         u[2].UID←⍕cid,uid←⊃(⍳1+≢queue)~queue[;1]
         u[2].Poll←poll
         payload←⎕JSON u
     :Else
         uid←0 ⋄ payload←u
     :EndIf
     'Queue is full'⎕SIGNAL(uid>999)/11
     queue←((queue[;1 2]∨.≠cid uid)⌿queue)⍪cid uid fn
 :EndHold

 :If 0≠⊃z←cid Send payload
     ⎕←'Send Failed'
     ∘∘∘
 :EndIf

 :If 0=≢fn
     r←cid Wait uid
 :Else
     r←runmonitor ⍬
 :EndIf
