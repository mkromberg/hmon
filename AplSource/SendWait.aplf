 r←{cbinfo}SendWait(cid u);i;uid;payload;z;fn;poll
 :If 0=⎕NC'cbinfo' ⋄ cbinfo←'' 0 ⋄ :EndIf
 (fn poll)←cbinfo
 :If 0≠≢fn
     ('Callback function ',fn,' not found')⎕SIGNAL(3≠⎕NC fn)/6
 :EndIf

 :Hold 'hmon'
     :If (2=≢u)∧326=⎕DR u
         u[2].UID←⍕cid,uid←⊃(⍳1+≢queue)~queue[;1]
         payload←⎕JSON u
     :Else
         uid←0 ⋄ payload←u
     :EndIf
     'Queue is full'⎕SIGNAL(uid>999)/11
     queue⍪←cid uid fn ⍝ Don't use callback fn for result of subscribe
 :EndHold

 :If 0≠⊃z←cid Send payload
     ⎕←'Send Failed'
     ∘∘∘
 :EndIf

 :If 0=≢fn ⍝ No callback function
     r←cid Wait uid
 :Else
     r←runmonitor ⍬
 :EndIf
