 DemoReceive arg;UID;Facts;data;TS;event;ts;cid;wsused;cpuused;compactions;ai;ws;line;fn;code;txt
 ⍝ Receive all types of callbacks

 (event data)←arg
 ts←1 ⎕DT'J'
 cid←2 1⊃⎕VFI UID←data.UID
 :If (≢demodata)≠≢conns
     demodata←(demodata[;1]∊conns[;1])⌿demodata
  :EndIf

 :Select event

 :Case 'Notification'
     Log UID,,' Notification ',data.Event

 :Case 'Facts'
     (ws ai)←data.(Facts[Facts.Name⍳'Workspace' 'AccountInformation'].Value)
     (wsused compactions)←ws.(Used Compactions)
     cpuused←ai.ComputeTime
     DemoUpdate cid('ts' 'wsused' 'cpuused' 'compactions')(ts wsused cpuused compactions)
     'DemoReceive' 2000 GetLastKnownState cid ⍝ Request last known state

 :Case 'LastKnownState'
     :If 0≠data.⎕NC'Activity'
         (code fn line)←data.(Activity.code,Location.(Function Line))
         txt←(⍕code),': ',fn,'[',(⍕line),']'
         DemoUpdate cid('ts' 'laststate')(ts txt)
     :EndIf
 :EndSelect
